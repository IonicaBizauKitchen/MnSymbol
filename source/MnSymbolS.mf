
font_identifier:="MnSymbolS";
font_size design_size * pt#;

mode_setup;
font_setup;

autorounding := hair;

input Sym-Init;
input calu;

font_slant    slant;
font_x_height x_height#;
font_quad     18u# + 4letter_fit#;

subs := interpolate(1)(5/5, 5/6, 5/7, 6/8, 6/9, 7/10, 8/12);  % subscript size
rth# := rule_thickness#;
fontdimen 8:  % parameters $\sigma_8$ through $\sigma_{22}$ will now be listed
 math_axis# + 3.51rth# + interpolate(1/36pt#)(36, 36, 36, 36, 36, 44, 53) + subs * desc_depth#,  % num1
 math_axis# + 1.51rth# + interpolate(1/36pt#)( 3,  9, 12, 24, 30, 30, 30),                       % num2
 math_axis# + 1.51rth# + interpolate(1/36pt#)(24, 29, 34, 38, 43, 48, 57),                       % num3
 interpolate(1/36pt#)(63, 74, 86, 98, 111, 155, 149) + 3.51rth# + subs * x_height# - math_axis#, % denom1
 interpolate(1/36pt#)( 3,  9, 12, 24,  30,  30,  30) + 1.51rth# + subs * x_height# - math_axis#, % denom2
 interpolate(pt#)(5.99, 6.49, 6.99, 7.49, 8.49, 8.99, 9.99) - subs * asc_height#, % sup1
 interpolate(pt#)(5.49, 5.99, 6.49, 6.99, 7.99, 8.49, 8.99) - subs * asc_height#, % sup2
 interpolate(1/36pt#)(53, 62, 72, 82, 93, 104, 125),              % sup3
 interpolate(1/36pt#)(36, 36, 36, 36, 36,  54,  65),              % sub1
 interpolate(1/36pt#)(72, 72, 72, 72, 57,  89, 100),              % sub2
 subs * asc_height# - 36/36pt#,                                   % sup\_drop
 18/36pt#,                                                        % sub\_drop
 interpolate(pt#)(9.9, 11.9, 11.9, 11.9, 19.0, 19.0, 22.0),       % delim1
 interpolate(pt#)(7.1,  8.1,  8.1,  9.1,  9.1, 10.1, 12.1),       % delim2
 math_axis#;              % axis\_height

beginchar(0, 0, 0, 0) % dummy symbol for skew
endchar;

skewchar = 0;
skew#    = .5u#;

ligtable "I": "T": "V": skewchar kern skew#;
ligtable "K": skewchar kern 2skew#;
ligtable "D": "N": "P": "R": "U": "W": "Y": skewchar kern 3skew#;
ligtable "E": "F": "G": "H": "O": "Q": skewchar kern 4skew#;
ligtable "B": "C": "L": "M": "S": "X": "Z": skewchar kern 5skew#;
ligtable "J": skewchar kern 6skew#;
ligtable "A": skewchar kern 7skew#;

bye;
