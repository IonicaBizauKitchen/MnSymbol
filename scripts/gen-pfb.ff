#!/usr/local/bin/fontforge
names=["A", "B", "C", "D", "E", "F", "A-Bold", "B-Bold", "C-Bold", "D-Bold", "E-Bold", "F-Bold"];
size=5;
while (size<=12)
fname=0;
while (fname<=11)
  Print("MnSymbol"+names[fname]+size);
  Open("MnSymbol"+names[fname]+size+".pfb");
  widths = Array(CharCnt());
  lbearings = Array(CharCnt());
  rbearings = Array(CharCnt());
  SelectAll();
  ii=0;
  foreach
    widths[ii] = GlyphInfo("Width");
    lbearings[ii] = GlyphInfo("LBearing");
    rbearings[ii] = GlyphInfo("RBearing");
    ii++;
  endloop
  Clear();
  ii=0;
  while(ii<=255)
    Select(ii);
    Import("MnSymbol"+names[fname]+size+"."+ii+".eps");
    SetWidth(widths[ii]);
    SetLBearing(lbearings[ii]);
    SetRBearing(rbearings[ii]);
    ii++;
  endloop
  SelectAll();
  DontAutoHint();
  if (names[fname]=="E" || names[fname]=="E-Bold")
    Print("RemoveOverlap in MnSymbolE");
    Select(204,208);
    RoundToInt();
    SelectAll();
    RemoveOverlap();
  endif
  CopyFgToBg();
  Save("MnSymbol"+names[fname]+size+".sfd");
#  RemoveOverlap();
#  AddExtrema();
#  Simplify();
#  Save("../test"+names[fname]+size+".sfd");
  fname++;
endloop

  size++;
  if (size==11)
    size++;
  endif
endloop

